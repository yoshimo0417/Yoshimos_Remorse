
IF
InParty(Myself)
Global("YYoshMakeGlobal","GLOBAL",0)
THEN
RESPONSE #100
MakeGlobal()
SetGlobal("YYoshMakeGlobal","GLOBAL",1)
END

IF
HPLT(Myself,2)
THEN
RESPONSE #100
CreateVisualEffectObject("SPFIRSDI",Myself)
DropInventory()
SetLeavePartyDialogueFile()
LeaveParty()
SetGlobal("YYStart","GLOBAL",8)
ApplySpell(Myself,RESTORE_FULL_HEALTH)
ChangeAIScript("",DEFAULT)
MoveGlobal("AR4500","YYosh25",[1600.1100])
END

//initial meeting

IF
Global("YYVARI","GLOBAL",0)
Global("YYStart","GLOBAL",4)
THEN
RESPONSE #100
SetGlobal("YYVARI","GLOBAL",1)
StartDialogueNoSet(Player1)
END

IF
Global("YYStart","GLOBAL",5)
Global("YYoshimoInfuence","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("YYInflu","GLOBAL",10)
StartDialogueNoSet(Player1)
END

//timered talks

IF
InParty(Myself)
Global("YYTalks","GLOBAL",0)
Global("YYoshimoRemorseActive","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("YYTalks","GLOBAL",1)
SetGlobal("YYoshimoRemorseActive","GLOBAL",1)
RealSetGlobalTimer("YYTime","GLOBAL",1600)
AddSpecialAbility("YYSHPCH")
END

IF
InParty(Myself)
CombatCounter(0)
See(Player1)
!StateCheck("YYOSH25",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("YYoshimoRemorseActive","GLOBAL",1)
RealGlobalTimerExpired("YYTime","GLOBAL")
OR(7)
Global("YYTalks","GLOBAL",1)
Global("YYTalks","GLOBAL",3)
Global("YYTalks","GLOBAL",5)
Global("YYTalks","GLOBAL",7)
Global("YYTalks","GLOBAL",9)
Global("YYTalks","GLOBAL",11)
Global("YYTalks","GLOBAL",13)
THEN
RESPONSE #100
StartDialog("YYosh25J",Player1)
END

IF
InParty(Myself)
CombatCounter(0)
See(Player1)
!StateCheck("YYOSH25",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("YYoshimoRemorseActive","GLOBAL",1)
RealGlobalTimerExpired("YYTime","GLOBAL")
OR(6)
Global("YYTalks","GLOBAL",2)
Global("YYTalks","GLOBAL",4)
Global("YYTalks","GLOBAL",6)
Global("YYTalks","GLOBAL",8)
Global("YYTalks","GLOBAL",10)
Global("YYTalks","GLOBAL",12)
THEN
RESPONSE #100
RealSetGlobalTimer("YYTime","GLOBAL",1600)
IncrementGlobal("YYTalks","GLOBAL",1)
END

//timered, Yoshimo was loves

IF
InParty(Myself)
Global("YYTalksLoved","GLOBAL",0)
Global("YYoshimoRemorseActive","GLOBAL",2)
Global("YYoshiWasLoved","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("YYTalksLoved","GLOBAL",1)
RealSetGlobalTimer("YYTime","GLOBAL",1600)
END

IF
InParty(Myself)
CombatCounter(0)
See(Player1)
!StateCheck("YYOSH25",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("YYoshiWasLoved","GLOBAL",1)
RealGlobalTimerExpired("YYTime","GLOBAL")
OR(2)
Global("YYTalksLoved","GLOBAL",1)
Global("YYTalksLoved","GLOBAL",3)
THEN
RESPONSE #100
StartDialog("YYosh25J",Player1)
END


IF
InParty(Myself)
CombatCounter(0)
See(Player1)
!StateCheck("YYOSH25",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("YYoshiWasLoved","GLOBAL",1)
RealGlobalTimerExpired("YYTime","GLOBAL")
OR(2)
Global("YYTalksLoved","GLOBAL",2)
Global("YYTalksLoved","GLOBAL",4)
THEN
RESPONSE #100
RealSetGlobalTimer("YYTime","GLOBAL",1600)
IncrementGlobal("YYTalksLoved","GLOBAL",1)
END

//Saradush talk

IF
InParty(Myself)
CombatCounter(0)
See(Player1)
!StateCheck("YYOSH25",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("SaradushSacked","GLOBAL",1)
Global("YYSaraushTalk","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("YYSaradushTalkTime","GLOBAL",300)
SetGlobal("YYSaraushTalk","GLOBAL",1)
END

IF
InParty(Myself)
CombatCounter(0)
See(Player1)
!StateCheck("YYOSH25",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("SaradushSacked","GLOBAL",1)
RealGlobalTimerExpired("YYSaradushTalkTime","GLOBAL")
Global("YYSaraushTalk","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("YYSaraushTalk","GLOBAL",2)
END

IF
Global("YYSaraushTalk","GLOBAL",2)
THEN
RESPONSE #100
StartDialog("YYosh25J",Player1)
END

//Poor House

IF
AreaCheck("AR5507")
InParty(Myself)
CombatCounter(0)
See(Player1)
!StateCheck("YYOSH25",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("YYPoorTalk","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("YYPoorTalk","GLOBAL",1)
END

IF
Global("YYPoorTalk","GLOBAL",1)
THEN
RESPONSE #100
StartDialog("YYosh25J",Player1)
END

//Encounter After Brother

IF 
InParty(Myself)
CombatCounter(0)
See(Player1)
!See("YYoshBro")
!StateCheck("YYOSH25",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("YYosh25BrotherEncounter","GLOBAL",2)
THEN
RESPONSE #100
StartDialog("YYosh25J",Player1)
END

//Final Talk
IF 
InParty(Myself)
CombatCounter(0)
See(Player1)
Dead("chalrv01")
!StateCheck("YYOSH25",CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("YYoshFinalTalk","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("YYoshFinalTalk","GLOBAL",1)
END

IF
Global("YYoshFinalTalk","GLOBAL",1)
THEN
RESPONSE #100
StartDialog("YYosh25J",Player1)
END 

//Malla's Soul Gem: PC saves Malla and lich kills Marlowe

IF
InParty(Myself)
CombatCounter(0)
See(Player1)
Global("MallaCured","GLOBAL",1)
Global("YYoshMallaTalk","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("YYoshMallaTalk","GLOBAL",1)
END

//Malla's Soul Gem: PC saves Malla and spares Marlowe

IF
InParty(Myself)
CombatCounter(0)
See(Player1)
Global("MarloweJob","GLOBAL",3)
Global("MallaGone","GLOBAL",1)
Global("YYoshMallaTalk","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("YYoshMallaTalk","GLOBAL",1)
END

//Malla's Soul Gem: PC takes Malla's soul for own use

IF
InParty(Myself)
CombatCounter(0)
See(Player1)
Global("MallaDead","GLOBAL",1)
Global("YYoshMallaTalk","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("YYoshMallaTalk","GLOBAL",1)
END

IF
Global("YYoshMallaTalk","GLOBAL",1)
THEN
RESPONSE #100
StartDialog("YYosh25J",Player1)
END 

//Avatar Second Encounter

IF
InParty(Myself)
CombatCounter(0)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("YYAvaSecondEncounter","GLOBAL",0)
Dead("ABAZIGAL")
Dead("SENDAI")
THEN
RESPONSE #100
SetGlobal("YYAvaSecondEncounter","GLOBAL",1)
SetGlobalTimer("YYAvaSecondTime","GLOBAL",ONE_DAY)
END 

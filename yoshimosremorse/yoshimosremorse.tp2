BACKUP ~YoshimosRemorse\backup~
AUTHOR ~Lava Del'Vortel and jmaeq~
README ~YoshimosRemorse/ReadMe-YR.html~
VERSION ~v1.3~	
	 
ALWAYS
  INCLUDE ~YoshimosRemorse/lib/eet.tph~
  INCLUDE ~YoshimosRemorse/lib/functions.tph~  
  INCLUDE ~YoshimosRemorse/lib/extra_regexp_vars.tph~
  ACTION_IF (GAME_IS ~eet~) BEGIN
    OUTER_SET bg2_chapter = 12
  END ELSE BEGIN
    OUTER_SET bg2_chapter = 0
  END
  OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
    OUTER_SET bg2_chapter = bg2_chapter + 1
    OUTER_SPRINT name_source ~bg2_chapter_%i%~
    OUTER_SET EVAL ~%name_source%~ = bg2_chapter
  END
  ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
    OUTER_SPRINT ~YOSHIMO_RACE_WRAITH~ ~ChangeRace("Yoshimo",WRAITH)~
  END ELSE BEGIN
    OUTER_SPRINT ~YOSHIMO_RACE_WRAITH~ ~~
  END
END

AUTO_TRA ~YoshimosRemorse\%s~

ALWAYS
  // No exception, everything needs to be converted
  ACTION_DEFINE_ARRAY yremorsenoconvert BEGIN END

  // any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
  ACTION_DEFINE_ARRAY yremorsereload BEGIN setup END

  LAF HANDLE_CHARSETS
    INT_VAR
      infer_charsets = 1
    STR_VAR
      tra_path = EVAL ~%MOD_FOLDER%/language~
      noconvert_array = yremorsenoconvert
      reload_array = yremorsereload
  END
END

AUTO_TRA ~YoshimosRemorse\Language\%s~
LANGUAGE ~English~ ~English~ ~%MOD_FOLDER%/Language/English/setup.tra~

BEGIN @1
  REQUIRE_PREDICATE (GAME_IS ~tob BGT bg2ee eet~) @2

//////////////////////////////////////////////////////////////////////////////////
////WILSON DOESN'T HAVE HIS BANTER FILE - THAT MAKES HIM SAD! LET'S CHANGE THAT///
//////////////////////////////////////////////////////////////////////////////////


ACTION_IF FILE_EXISTS_IN_GAME ~wilson.dlg~ THEN BEGIN
 PRINT @3
    APPEND ~interdia.2da~ ~WILSON BWILSON BWILSO25~
    UNLESS ~WILSON~
   ACTION_IF NOT (FILE_EXISTS_IN_GAME ~BWILSON.dlg~) THEN BEGIN
   COMPILE ~%MOD_FOLDER%/dialogues/BWILSONFix.d~
  END
   ACTION_IF NOT (FILE_EXISTS_IN_GAME ~BWILSO25.dlg~) THEN BEGIN
   COMPILE ~%MOD_FOLDER%/dialogues/BWILSO25Fix.d~
  END
END


/////////PORTRAITS

ACTION_IF GAME_IS ~BG2EE eet~ THEN BEGIN
	COPY ~YoshimosRemorse/PORTRAITS~ ~Override~
END ELSE BEGIN
	COPY ~YoshimosRemorse/PORTRAITS/BG2~ ~Override~
END

/////////AREAS

COPY ~YoshimosRemorse/AREAS~ ~Override~

//AUDIO

COPY ~YoshimosRemorse/AUDIO~ ~Override~

LAF HANDLE_AUDIO END

/////////DIALOGUES

COMPILE EVALUATE_BUFFER ~YoshimosRemorse/DIALOGUES/Start.d~
COMPILE EVALUATE_BUFFER ~YoshimosRemorse/DIALOGUES/YYosh25J.d~
COMPILE EVALUATE_BUFFER ~YoshimosRemorse/DIALOGUES/YYosh25B.d~
ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~YoshimosRemorse/DIALOGUES/YYosh25B_EE.d~
END
COMPILE EVALUATE_BUFFER ~YoshimosRemorse/DIALOGUES/YYoshBro.d~

/////////SCRIPTS

COMPILE EVALUATE_BUFFER ~YoshimosRemorse/SCRIPTS/YYAva.baf~
COMPILE EVALUATE_BUFFER ~YoshimosRemorse/SCRIPTS/YYosh25.baf~
/* XP adjustment in extra files because there is no ChangeStat in classic engine */
ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
  EXTEND_BOTTOM ~YYosh25.bcs~ ~YoshimosRemorse/SCRIPTS/YYosh25_addEE.baf~ EVALUATE_BUFFER 
END ELSE BEGIN
  EXTEND_BOTTOM ~YYosh25.bcs~ ~YoshimosRemorse/SCRIPTS/YYosh25_add.baf~ EVALUATE_BUFFER 
END
COMPILE EVALUATE_BUFFER ~YoshimosRemorse/SCRIPTS/YYosh25d.baf~
COMPILE EVALUATE_BUFFER ~YoshimosRemorse/SCRIPTS/YYCut1.baf~
COMPILE EVALUATE_BUFFER ~YoshimosRemorse/SCRIPTS/YYCut2.baf~
COMPILE EVALUATE_BUFFER ~YoshimosRemorse/SCRIPTS/YYCut2a.baf~
COMPILE EVALUATE_BUFFER ~YoshimosRemorse/SCRIPTS/YYBro.baf~
COMPILE EVALUATE_BUFFER ~YoshimosRemorse/SCRIPTS/YYCut3.baf~
COMPILE EVALUATE_BUFFER ~YoshimosRemorse/SCRIPTS/YYCut4.baf~
COMPILE EVALUATE_BUFFER ~YoshimosRemorse/SCRIPTS/YYAR01.baf~

EXTEND_TOP ~AR2900.bcs~  ~YoshimosRemorse/SCRIPTS/AR2900.baf~ EVALUATE_BUFFER
EXTEND_TOP ~AR4500.bcs~  ~YoshimosRemorse/SCRIPTS/AR4500.baf~ EVALUATE_BUFFER
EXTEND_TOP ~AR5500.bcs~  ~YoshimosRemorse/SCRIPTS/AR5500.baf~ EVALUATE_BUFFER
EXTEND_TOP ~AR6200.bcs~  ~YoshimosRemorse/SCRIPTS/AR6200.baf~ EVALUATE_BUFFER

/////////EPILOGUES

COPY ~YoshimosRemorse/EPILOGUES/YYoshE.2da~ ~override/YYosh01.2da~
REPLACE ~99999~ @4

COPY ~YoshimosRemorse/EPILOGUES/YYoshE.2da~ ~override/YYosh02.2da~
REPLACE ~99999~ @5

COPY ~YoshimosRemorse/EPILOGUES/YYoshE.2da~ ~override/YYosh03.2da~ 
REPLACE ~99999~ @6

/////////SPELL

COPY ~YoshimosRemorse/SPELLS/YYSETIT.spl~ ~Override~
COPY ~YoshimosRemorse/SPELLS/YYSHPCH.spl~ ~Override~
SAY NAME1 @7
SAY NAME2 @7
SAY UNIDENTIFIED_DESC @8
/////////CREATURES

ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
  OUTER_SET wraith_lcase = RESOLVE_STR_REF(~wraith~)
  OUTER_SET wraith_ucase = RESOLVE_STR_REF(~Wraith~)
  APPEND ~racetext.2da~ ~WRAITH   134        %wraith_lcase%       -1         %wraith_ucase%          -1~ UNLESS ~WRAITH~
END


COPY ~YoshimosRemorse/CREATURES/YYosh25.cre~ ~Override/YYosh25.cre~
  SAY MORALE @11
  SAY HAPPY @12
  SAY UNHAPPY_ANNOYED @13
  SAY UNHAPPY_SERIOUS @14
  SAY UNHAPPY_BREAKING_POINT @15
  SAY LEADER @16
  SAY TIRED @17
  SAY BORED @18
  SAY BATTLE_CRY1 @19
  SAY BATTLE_CRY2 @20
  SAY BATTLE_CRY3 @21
  SAY BATTLE_CRY4 ~~
  SAY BATTLE_CRY5 ~~
  SAY DAMAGE @22
  SAY DYING @23
  SAY HURT @24
  SAY AREA_FOREST @25
  SAY AREA_CITY @26
  SAY AREA_DUNGEON @27
  SAY AREA_DAY @28
  SAY AREA_NIGHT @29
  SAY SELECT_COMMON1 @30
  SAY SELECT_COMMON2 @31
  SAY SELECT_COMMON3 @32
  SAY SELECT_COMMON4 @33
  SAY SELECT_COMMON5 @34
  SAY SELECT_COMMON6 @35
  SAY SELECT_ACTION1 @36
  SAY SELECT_ACTION2 @37
  SAY SELECT_ACTION3 @38
  SAY SELECT_ACTION4 @39
  SAY SELECT_ACTION5 @40
  SAY SELECT_ACTION6 @41
  SAY SELECT_ACTION7 @42
  SAY SELECT_RARE1 @43
  SAY SELECT_RARE2 @44
  SAY CRITICAL_HIT @45
  SAY CRITICAL_MISS @46
  SAY TARGET_IMMUNE @47
  SAY INVENTORY_FULL @48
  SAY HIDDEN_IN_SHADOWS @49
  SAY PICKED_POCKET @50
  SAY SET_A_TRAP @51
  SAY SPELL_DISRUPTED @52

COPY ~YoshimosRemorse/CREATURES/YYava.cre~ ~Override/YYava.cre~
SAY NAME1 ~Messenger~
SAY NAME2 ~Messenger~

COPY ~YoshimosRemorse/CREATURES/yyoshbro.cre~ ~Override/yyoshbro.cre~
SAY NAME1 ~Kagetaro~
SAY NAME2 ~Kagetaro~

//               					    POST_DIALOG_FILE JOIN_DIALOG_FILE DREAM_SCRIPT_FILE 25POST_DIALOG_FILE 25JOIN_DIALOG_FILE 25DREAM_SCRIPT_FILE   25OVERRIDE_SCRIPT_FILE
COPY_EXISTING ~pdialog.2da~ ~override~
  REPLACE_TEXTUALLY ~YOSHIMO[%tab% %lnl%%mnl%%wnl%]+YOSHP[%tab% %lnl%%mnl%%wnl%]+YOSHJ[%tab% %lnl%%mnl%%wnl%]+YOSHD[%tab% %lnl%%mnl%%wnl%]+YOSHP[%tab% %lnl%%mnl%%wnl%]+YOSHJ[%tab% %lnl%%mnl%%wnl%]+YOSHD[%tab% %lnl%%mnl%%wnl%]+yosh25~
					~YOSHIMO                  YOSHP                 YOSHJ                 YOSHD                 YYosh25P                 YYosh25J               YYosh25D               YYosh25~
//         					   FILE      25FILE
COPY_EXISTING ~interdia.2da~ ~override~
  REPLACE_TEXTUALLY ~YOSHIMO[%tab% %lnl%%mnl%%wnl%]+BYOSHIM[%tab% %lnl%%mnl%%wnl%]+BYOSHIM~
					~YOSHIMO   BYOSHIM   YYOSH25B~

/////////BAMS

COPY ~YoshimosRemorse\BAMS~ ~override\~

/////////ITEMS

COPY ~YoshimosRemorse/ITEMS/YYSwo1.itm~ ~Override/YYSwo1.itm~
SAY NAME1 @53
SAY NAME2 @53
SAY DESC @54

COPY ~YoshimosRemorse/ITEMS/YYCLO1.itm~ ~Override/YYCLO1.itm~
SAY NAME1 @55
SAY NAME2 @55
SAY DESC @56

ACTION_IF GAME_IS ~BG2EE EET~ THEN BEGIN
COPY_EXISTING ~YYSwo1.itm~ ~Override~
  LPF ADD_ITEM_EQEFFECT
    INT_VAR
      opcode = 319
	  target = 1
	  timing = 2
	  parameter2 = 11
	  power = 1
	  special = RESOLVE_STR_REF ( ~Yoshimo~ )
	STR_VAR
	  resource = YOSHIMO
END
SAY DESC @57

COPY_EXISTING ~YYCLO1.itm~ ~Override~
  LPF ADD_ITEM_EQEFFECT
    INT_VAR
      opcode = 319
	  target = 1
	  timing = 2
	  parameter2 = 11
	  power = 1
	  special = RESOLVE_STR_REF (~Yoshimo~)
	STR_VAR
	  resource = YOSHIMO
END
END

BEGIN @59
REQUIRE_COMPONENT ~YoshimosRemorse.tp2~ ~0~ @58
SUBCOMPONENT @61
COPY ~YoshimosRemorse/CREATURES/YYOSH10.cre~ ~Override/YYosh25.cre~

BEGIN @60
REQUIRE_COMPONENT ~YoshimosRemorse.tp2~ ~0~ @58
SUBCOMPONENT @61
COPY ~YoshimosRemorse/CREATURES/YYOSH13.cre~ ~Override/YYosh25.cre~
